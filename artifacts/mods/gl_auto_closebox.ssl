/*

Auto-Close Containers Mod
----------------------------------------------------
- automatically closes the container after looting

   Requires sfall 4.2.2/3.8.29 or higher

*/

#include "main.h"

procedure start;

variable  lootObject;

procedure start begin
   if (game_loaded) then begin
      if (GetConfig("Inventory", "AutoCloseContainers", 0)) then register_hook_proc_spec(HOOK_GAMEMODECHANGE, start);
   end
   else begin
      variable mode = get_game_mode;
      if (mode andAlso mode != INTFACELOOT) then return;
      if (mode == INTFACELOOT) then begin
         lootObject = loot_obj;
         if (obj_type(lootObject) != OBJ_TYPE_ITEM) then lootObject = 0;
      end
      else if (lootObject andAlso get_sfall_arg_at(1) == INTFACELOOT) then begin
         obj_close(lootObject);
         lootObject = 0;
      end
   end
end